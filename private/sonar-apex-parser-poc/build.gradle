plugins {
    id 'java'
    id "de.undercouch.download" version "3.4.3"
}

group 'org.sonarsource.apex'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

def apexCodeEditorVersion = '44.2.0'
def apexJorjeLspJar = new File(buildDir, 'libs/apex-jorje-lsp-' + apexCodeEditorVersion + '.jar')

repositories {
    mavenCentral()
}

dependencies {
    compile files(apexJorjeLspJar)
    testCompile group: 'junit', name: 'junit', version: '4.13.1'
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.6.1'
}

task downloadDependency(type: Download) {
    onlyIf {task -> !apexJorjeLspJar.exists()}
    src 'https://github.com/forcedotcom/salesforcedx-vscode/raw/v' + apexCodeEditorVersion + '/packages/salesforcedx-vscode-apex/out/apex-jorje-lsp.jar'
    dest apexJorjeLspJar
    overwrite true
    onlyIfModified true
}

task downloadAndVerifyDependency(type: Verify) {
    src apexJorjeLspJar
    algorithm 'MD5'
    checksum '12f6898fbfcc49fbd1326144c5d3687c'
}

downloadAndVerifyDependency.dependsOn downloadDependency
build.dependsOn downloadAndVerifyDependency
