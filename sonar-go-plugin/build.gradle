plugins {
    id 'com.github.johnrengelman.shadow' version '4.0.2'
}

// require sonar-go-to-slang binaries to be build
shadowJar.dependsOn ':sonar-go-to-slang:build'
test.dependsOn ':sonar-go-to-slang:build'

dependencies {
    compile project(':slang-api')
    compile project(':slang-checks')
    compile project(':slang-plugin')
    compile project(':checkstyle-import')
    // dependency on sonar-go-to-slang binaries
    runtime files(project(':sonar-go-to-slang').buildDir)
    compileOnly 'org.sonarsource.sonarqube:sonar-plugin-api'
    compile 'org.sonarsource.analyzer-commons:sonar-analyzer-commons'
    compile 'com.eclipsesource.minimal-json:minimal-json'
    testCompile 'junit:junit'
    testCompile 'org.assertj:assertj-core'
    testCompile 'org.mockito:mockito-core'
    testCompile project(':slang-antlr')
    testCompile project(':slang-testing')
    testCompile 'org.awaitility:awaitility:3.1.0'
}

tasks.withType(JavaCompile) {
    // Prevent warning: Gradle 5.0 will ignore annotation processors
    options.compilerArgs += [ "-proc:none" ]
}

test {
    testLogging {
        exceptionFormat 'full' // log the full stack trace (default is the 1st line of the stack trace)
        events "skipped", "failed" // verbose log for failed and skipped tests (by default the name of the tests are not logged)
    }
}

jar {
    manifest {
        def displayVersion = (project.buildNumber == null ? version : version.substring(0, version.lastIndexOf('.')) + " (build ${project.buildNumber})")
        def buildDate = new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        attributes(
                'Build-Time': buildDate,
                'Implementation-Build': 'git rev-parse HEAD'.execute().text.trim(),
                'Plugin-BuildDate': buildDate,
                'Plugin-ChildFirstClassLoader': 'false',
                'Plugin-Class': 'org.sonar.go.plugin.GoPlugin',
                'Plugin-Description': 'Code Analyzer for Go',
                'Plugin-Developers': 'SonarSource Team',
                'Plugin-Display-Version': displayVersion,
                'Plugin-Homepage': 'http://redirect.sonarsource.com/plugins/go.html',
                'Plugin-IssueTrackerUrl': 'https://jira.sonarsource.com/browse/SONARSLANG',
                'Plugin-Key': 'go',
                'Plugin-License': 'GNU LGPL 3',
                'Plugin-Name': 'Go Code Quality and Security',
                'Plugin-Organization': 'SonarSource',
                'Plugin-OrganizationUrl': 'http://www.sonarsource.com',
                'Plugin-SourcesUrl': 'https://github.com/SonarSource/slang',
                'Plugin-Version': version,
                'Sonar-Version': '6.7',
                'SonarLint-Supported': 'false',
                'Version': "${version}",
        )
    }
}

shadowJar {
    minimize { }
    dependencies {
        exclude(dependency('org.sonarsource.sonarqube:sonar-plugin-api'))
        exclude(dependency('org.codehaus.woodstox:.*'))
        exclude(dependency('org.codehaus.staxmate:.*'))
        exclude(dependency('com.google.code.findbugs:jsr305'))

        exclude 'libs/**'
        exclude 'META-INF/maven/**'
        exclude 'tmp/**'
    }
    doLast {
        enforceJarSizeAndCheckContent(new File(libsDir, shadowJar.archiveName), 6_600_000L, 7_100_000L)
    }
}

artifacts {
    archives shadowJar
}

artifactoryPublish.skip = false

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact source: shadowJar, classifier: null
            artifact sourcesJar
            artifact javadocJar
        }
    }
}
